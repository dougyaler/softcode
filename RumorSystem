@@ Self-deteriorating and deleting Rumors System 

@@ +rumors OR +rumours - Displays all categories of rumors and their total in each.
@@ +rumors <category>    - Displays all rumors within that particular category.
@@ +rumors/add <category>=<text> - Adds a new rumor within that particular category.
@@ +rumors/del <category>/<number> - Deletes a certain rumor- limited to those who return 1 for CAN or the person who started the rumor.

@@ CAT is just an attribute tree branch.
@@ CAT`<category> is the branch for a category.
@@ CAT`<category>`<number> is an individual rumor, sorted by number.
@@ CAT`<categpry>`<number>`READ lists dbrefs of all players who have read that specific rumor.
@@ CAT`<category>`<number>`BY is the person who started the rumor.

@@ Whenever a rumor is read within its category, each rumor is checked to see if the enactor has read each rumor. If so, nothing more will happen. If not, the enactor is added to the *`BY attribute and the rumor is checked to see if 1) It's a third read, meaning a random non-ellipsed word gets replaced with ellipses 2) It gets purged due to being composed of 60% ellipses.

@@ +rumors/newcategory and /deletecategory should be self explanatory- only those who pass CAN are able to do this.

@@ Rumors System
@create Rumors System
@link Rumors System = #2
&CAN Rumors System=1
&CAT Rumors System=Attribute Tree: Categories of All Rumors
&CAT`CELEBRITY Rumors System=0
&CAT`CELEBRITY`1 Rumors System=\"Hear the latest?" "Whassup?" "I heard that that one famous person is a big fat phony!" "No way!" "Yah way!"
&CAT`CELEBRITY`1`BY Rumors System=#11032
&CAT`CRIME Rumors System=0
&CAT`CRIME`1 Rumors System=\"Didja hear? Crime out outta control?" "No it's not. Are you listening to the tabloids again?"
&CAT`CRIME`1`BY Rumors System=#11032
&CAT`GOVERNMENT Rumors System=0
&CAT`GOVERNMENT`1 Rumors System=\THEY'RE LYING TO YOU! THE GOVERNMENT IS CONTROLLED BY LIZARD PEOPLE! FREE YOUR MIND!
&CAT`GOVERNMENT`1`BY Rumors System=#11032
&CMD Rumors System=Attribute Tree: $-Command Attributes
&CMD`ADD Rumors System=$^\+rumo?(u)?rs/add (.+)=(.+)$: @switch/first [hasattr(me,CAT`%2)]=1,{@pemit %#=Your rumor has been added to the [capstr(lcstr(%2))] category successfully.[ufun(DO`ADD,%2,%3,%#)]},{@pemit %#=That is not a valid category.}
@set Rumors System/CMD`ADD=regexp
&CMD`CATEGORYADD Rumors System=$^\+rumo?(u)?rs/newcategory (.+)$: @switch/first [u(CAN,%#)]:[hasattr(me,CAT`%2)]=0:?,{@pemit %#=Rumor has it that you can't do that.},?:1,{@pemit %#=The category '%2' already exists.},1:0,{@pemit %#=Added rumor category: %2.[attrib_set(me/CAT`%2,0)]},{@pemit %#=If else returned for: #$}
@set Rumors System/CMD`CATEGORYADD=regexp
&CMD`CATEGORYDEL Rumors System=$^\+rumo?(u)?rs/delcategory (.+)$: @switch/first [u(CAN,%#)]:[hasattr(me,CAT`%2)]=0:?,{@pemit %#=Rumor has it that you can't do that.},?:0,{@pemit %#=The category '%2' doesn't even exist.},1:1,{@pemit %#=Removed rumor category: %2.[wipe(me/CAT`%2****)]},{@pemit %#=If else returned for: #$}
@set Rumors System/CMD`CATEGORYDEL=regexp
&CMD`DELETE Rumors System=$^\+rumo?(u)?rs/del (.+)/(.+)$: @switch/first [t(hasattr(me,CAT`%2))]:[t(hasattr(me,CAT`%2`%3))]:[or(u(CAN,%#),t(match(get(me/CAT`%2`%3`BY),%#)))]=0:?:?,{@pemit %#=Invalid rumor category: %2},?:0:?,{@pemit %#=Invalid number in [ucstr(%2)] category: %3.},?:?:0,{@pemit %#=Rumor has it that you're unauthorized to do that.},1:1:1,{@pemit %#=Success.},{@pemit %#=If else returned for: #$.}
@set Rumors System/CMD`DELETE=regexp
&CMD`READ Rumors System=$^\+rumo?(u)?rs (.+)$: @switch/first [hasattr(me,CAT`%2)]=1,{@pemit %#=[center(%b%b[capstr(lcstr(%2))] Rumors%b%b,75,=)]%r%r[iter(sort(lattr(me/CAT`%2`)),[ansi(h,setr(1,last(##,`)))]: [u(##)][ufun(DO`READ,CAT`%2`%q1,owner(%#))],,%r)]%r%r[repeat(=,75)]},{@pemit %#=That is not a valid category.}@set Rumors System/CMD`READ=regexp
@set Rumors System/CMD`READ=regexp
&CMD`RUMORS Rumors System=$^\+rumo?(u)?rs$: @pemit %#=[center(%b%bCategories%b%b,75,=)]%r[setq(0,iter(lattr(me/CAT`),capstr(lcstr(after(##,`))),,|))]%r[table(iter(%q0,{## ([nattr(me/CAT`##`)] rumors)},|,|),35,75,|)]%r%r[repeat(=,75)]
@set Rumors System/CMD`RUMORS=regexp
&DESCRIBE Rumors System=%r[edit(center(ansi(hw,%b%bCommand attributes%b%b),78,_),_,ansi(hx,_))]%r[table(setr(0,lattr(me/CMD****)),17,78)]%r[edit(center(ansi(hw,%b%bFunction attributes%b%b),78,_),_,ansi(hx,_))]%r[table([setr(1,[lattr(me/FN****)] [lattr(me/FUN****)])],17,78)]%r[edit(center(ansi(hw,%b%bOther attributes%b%b),78,_),_,ansi(hx,_))]%r[table(setdiff(remove(lattr(me/*****),DESCRIBE),%q1 %q0),17,78)]%r%rTotal of: [nattr(me/********)] attributes.
@set Rumors System/DESCRIBE=no_command visual prefixmatch public nearby
&DO Rumors System=Attribute Tree: Execute Functions
&DO`ADD Rumors System=[setq(1,escape(%1))][setq(2,inc(v(CAT`%0)))][attrib_set(me/CAT`%0`%q2,%q1)][attrib_set(me/CAT`%0`%q2`BY,%#)]
&DO`DEGRADE Rumors System=[setq(1,pickrand(ldelete(lnum(1,words(setr(0,v(%0)))),matchall(%q0,...))))] [if(match(elements(%q0,%q1),"*"),setq(2,M),setq(2,switch(elements(%q0,%q1),"*,B,*",A,N)))] [attrib_set(me/%0,replace(%q0,%q1,[switch(%q2,M,",B,")]...[switch(%q2,M,",A,")]))]
&DO`DIS Rumors System=[center(%b%b[capstr(lcstr(%0))] Rumors%b%b,75,=)]%r%r[iter(lattr(me/CAT`%0`),[last(##,`)]: [u(##)],,%r)]%r%r[repeat(=,75)]
&DO`PURGE Rumors System=[wipe(me/%0)]
&DO`READ Rumors System=[if(t(member(setr(2,v(%0`READ)),owner(%#))),,[attrib_set(me/%0`READ,%q2 [owner(%#)])][if(t(match(fdiv(words(v(%0`READ)),3),*.*)),,ufun(DO`DEGRADE,%0))])][if(gte(mul(fdiv(words(matchall(setr(0,v(%0)),...)),words(%q0)),100),60),ufun(DO`PURGE,%0))]
&CREDITS Rumors System=Coded by moniker@M*U*S*H - Use with permission.
@set Rumors System/CREDITS=VISUAL

THINK Edit the CAN attribute to who can delete any rumor (aside from its creator) and add/delete new categories-- as of now ANYONE can do this.
THINK Example: &CAN Rumors System=orflags(%0,Wr)
THINK A sampling of categories have been added for testing purposes, feel free to /delcategory these at your leisure.
